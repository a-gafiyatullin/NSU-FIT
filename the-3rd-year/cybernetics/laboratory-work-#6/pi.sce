//----- непрерывный случай ---------------------------------------------
T0 = 0.76;
n = 3;
Tau = 0;
K = 1.5;
Ti = 2.33;

s = poly(0, 's');
W1 = K * (1 + 1 / (s * Ti)) * ((-Tau * s + 2 * n)^n / (Tau * s + 2 * n)^n) / (1 + s * T0)^n;
W = W1 / (1 + W1);
Sys = syslin('c', W);

// получаем матрицы A, B, C, D из КНФ
[A, B, C, D] = abcd(Sys);
// уравнение Ляпунова
H = lyap(A, -eye(A), 'c');
I = spec(H);
disp(I);
// вычисление запаса устойчивости
if I > 0 then
    kappa = norm(H, 2);
else
    kappa = %inf;
end;
printf("непрывный случай: ||H|| = %f", kappa);

//----- дискретный случай ----------------------------------------------
T0 = 0.76;
n = 3;
Tau = 1.2;
K = 0.481194691128603401778886212513064354113197630437637476688;
Ti = 1.30705212;

h = Tau / 100;

// дискретизация
s = poly(0, 's');
W1 = K * (1 + 1 / (s * Ti)) * ((-Tau * s + 2 * n)^n / (Tau * s + 2 * n)^n) / (1 + s * T0)^n;
W = W1 / (1 + W1);
Sys = syslin('c', W);
// получаем матрицы A, B, C, D из КНФ
Sysd = dscr(Sys, h);
// уравнение Ляпунова
H = lyap(Sysd.A, -eye(Sysd.A), 'd');
I = spec(H);
disp(I);
// вычисление запаса устойчивости
if I > 0 then
    kappa = norm(H, 2);
else
    kappa = %inf;
end;
printf("дискретный случай: ||H|| = %f", kappa);



